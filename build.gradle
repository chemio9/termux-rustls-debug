buildscript {
    repositories {
        maven { url "https://jitpack.io" }
        mavenCentral()
        google()
        gradlePluginPortal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:8.8.2"
        classpath 'org.mozilla.rust-android-gradle:plugin:0.9.0'
    }
}


allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven {
            url = findRustlsPlatformVerifierProject()
            metadataSources.artifact()
        }
    }
}

import groovy.json.JsonSlurper
String findRustlsPlatformVerifierProject() {
    var PATH_TO_DEPENDENT_CRATE = "./app/src/main/rust/rustls_integration"
    def dependencyText = providers.exec {
        // print now working directory
        commandLine("cargo", "metadata", "--format-version", "1", "--manifest-path", "$PATH_TO_DEPENDENT_CRATE/Cargo.toml")
    }.standardOutput.asText.get()

    def dependencyJson = new JsonSlurper().parseText(dependencyText)
    def manifestPath = file(dependencyJson.packages.find { it.name == "rustls-platform-verifier-android" }.manifest_path)
    return new File(manifestPath.parentFile, "maven").path
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
